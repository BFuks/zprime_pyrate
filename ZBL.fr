(***********************************************************************************)
(******     FeynRules model file for a Z' extension of the Standard model     ******)
(******                                                                       ******)
(******     Author: B. Fuks                                                   ******)
(******                                                                       ******)
(***********************************************************************************)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)

M$ModelName = "ZBL";
M$Information = {Authors -> {"B. Fuks"}, Institutions -> {"LPTHE / Sorbonne U."}, Version -> "1.0", Date -> "24.04.2024", Emails -> {"fuks@lpthe.jussieu.fr"} };
FeynmanGauge = False;

M$InteractionOrderHierarchy = { {QCD, 1},  {QED, 2}  };
M$InteractionOrderLimit     = { {QCD, 99}, {QED, 99} };


(* ************************** *)
(* *****      vevs      ***** *)
(* ************************** *)
M$vevs = { {phi[2],vphi}, {chi, vchi} };


(* ************************** *)
(* *****    mixings     ***** *)
(* ************************** *)

M$MixingsDescription = {
   Mix["1a"] == {MassBasis -> {A, Z, Zp}, GaugeBasis -> {B, Wi[3], Bp}, MixingMatrix -> UG, BlockName -> NEUTRALEWMIX},
   Mix["1b"] == {MassBasis -> {W, Wbar},  GaugeBasis -> {Wi[1], Wi[2]}, MixingMatrix -> UW, BlockName -> CHARGEDEWMIX},

   Mix["2a"] == {MassBasis -> {GP}, GaugeBasis -> {phi[1]},Value -> {{-I}}},
   Mix["2b"] == {MassBasis -> {{h1, h2}, {G0, G0p}}, GaugeBasis -> {phi[2], chi}, MixingMatrix -> {UHN,UAN}, BlockName -> {HMIX,AMIX} },

   Mix["3a"] == {
     MixingMatrix -> {UDL, UDR}, BlockName -> {LDOWNMIX, RDOWNMIX},
     MassBasis -> {dq[1, _], dq[2, _], dq[3, _]},
     GaugeBasis -> {{QL[2, 1, _], QL[2, 2, _], QL[2, 3, _]}, {dR[1, _], dR[2, _], dR[3, _]}}
   },
   Mix["3b"] == {
     MixingMatrix -> {UUL, UUR}, BlockName -> {LUPMIX, RUPMIX},
     MassBasis -> {uq[1, _], uq[2, _], uq[3, _]},
     GaugeBasis -> {{QL[1, 1, _], QL[1, 2, _], QL[1, 3, _]}, {uR[1, _], uR[2, _], uR[3, _]}}
   },

   Mix["4a"] == {
    MixingMatrix -> {ULL, ULR}, BlockName -> {LLMIX, RLMIX},
    MassBasis -> {l[1], l[2], l[3]}, GaugeBasis -> {{LL[2, 1], LL[2, 2], LL[2, 3]}, {lR[1], lR[2], lR[3]}}
   },
   Mix["4b"] == {
     MassBasis -> {vl[1], vl[2], vl[3], Nl[1], Nl[2], Nl[3]},
     GaugeBasis -> { {CC[LL][1,1], CC[LL][1,2], CC[LL][1,3], vR[1], vR[2], vR[3]}, {LL[1,1], LL[1,2], LL[1,3], CC[vR][1], CC[vR][2], CC[vR][3]} },
     MixingMatrix -> {PMNSL, PMNSR}, BlockName -> {PMNSMIX, PMNSRMIX}
   }
};


(* ************************** *)
(* *****  Gauge groups  ***** *)
(* ************************** *)
M$GaugeGroups = {
  U1Y  == { Abelian -> True, CouplingConstant -> g1, GaugeBoson -> B,  Charge -> Y, PyrateTag -> U1 },

  SU2L == {
    Abelian -> False, CouplingConstant -> gw, GaugeBoson -> Wi,
    StructureConstant -> Eps, Representations -> {Ta,SU2D},  Definitions -> {Ta[a_,b_,c_]->PauliSigma[a,b,c]/2, FSU2L[i_,j_,k_]:> I Eps[i,j,k]},
    PyrateTag -> SU2
  },

  SU3C == {
    Abelian -> False, CouplingConstant -> gs, GaugeBoson -> G,
    StructureConstant -> f, Representations -> {T,Colour}, SymmetricTensor -> dSUN,
    PyrateTag -> SU3
  },

  U1p  == { Abelian -> True, CouplingConstant -> gp, GaugeBoson -> Bp,  Charge -> Yp, PyrateTag -> U1 }
};

PyrateRep[SU2W  ] = 3;
PyrateRep[SU2D  ] = 2;
PyrateRep[Gluon ] = 8;
PyrateRep[Colour] = 3;


(* ************************** *)
(* *****    Indices     ***** *)
(* ************************** *)
IndexRange[Index[SU2W      ]] = Unfold[Range[3]];         IndexStyle[SU2W,       j];
IndexRange[Index[SU2D      ]] = Unfold[Range[2]];         IndexStyle[SU2D,       k];
IndexRange[Index[Gluon     ]] = NoUnfold[Range[8]];       IndexStyle[Gluon,      a];
IndexRange[Index[Colour    ]] = NoUnfold[Range[3]];       IndexStyle[Colour,     m];
IndexRange[Index[Generation]] = Range[3];                 IndexStyle[Generation, f];


(* ************************** *)
(* **** Particle classes **** *)
(* ************************** *)
M$ClassesDescription = {

  (* Gauge bosons: physical vector fields *)
  V[1] == { ClassName->A,  SelfConjugate->True,  Mass->0,   Width->0,             ParticleName->"a",  PDG->22},
  V[2] == { ClassName->Z,  SelfConjugate->True,  Mass->MZ,  Width->{WZ, 2.4952},  ParticleName->"Z",  PDG->23},
  V[3] == { ClassName->Zp, SelfConjugate->True,  Mass->MZp, Width->{WZp, 10.},    ParticleName->"Zp", PDG->32},
  V[4] == { ClassName->W,  SelfConjugate->False, Mass->MW,  Width->{WW, 2.085},   ParticleName->"W+", AntiParticleName->"W-", QuantumNumbers->{Q->1}, PDG->24},
  V[5] == { ClassName->G, SelfConjugate->True, Indices->{Index[Gluon]}, Mass->0, Width->0, ParticleName->"g", PDG->21},

  (* Ghosts: related to physical gauge bosons *)
  U[1] == { ClassName->ghA,  SelfConjugate->False, Ghost->A,    QuantumNumbers->{GhostNumber->1}, Mass->0,   Width->0,           ParticleName->"ghA" },
  U[2] == { ClassName->ghZ,  SelfConjugate->False, Ghost->Z,    QuantumNumbers->{GhostNumber->1}, Mass->MZ,  Width->{WZ,2.4952}, ParticleName->"ghZ" },
  U[3] == { ClassName->ghZp, SelfConjugate->False, Ghost->Zp,   QuantumNumbers->{GhostNumber->1}, Mass->MZp, Width->{WZp,10.},   ParticleName->"ghZp"},
  U[4] == { ClassName->ghWp, SelfConjugate->False, Ghost->W,    QuantumNumbers->{GhostNumber->1, Q->1 }, Mass->MW, Width->{WW, 2.085}, ParticleName->"ghWp"},
  U[5] == { ClassName->ghWm, SelfConjugate->False, Ghost->Wbar, QuantumNumbers->{GhostNumber->1, Q->-1}, Mass->MW, Width->{WW, 2.085}, ParticleName->"ghWm"},
  U[6] == { ClassName->ghG,  SelfConjugate->False, Indices->{Index[Gluon]}, Ghost->G, QuantumNumbers->{GhostNumber->1}, Mass->0, Width->0, ParticleName->"ghG"},

  (* Gauge bosons: unphysical vector fields *)
  V[11] == { ClassName->B,  Unphysical->True, SelfConjugate->True },
  V[12] == { ClassName->Wi, Unphysical->True, SelfConjugate->True, Indices->{Index[SU2W]}, FlavorIndex->SU2W },
  V[13] == { ClassName->Bp, Unphysical->True, SelfConjugate->True },

  (* Ghosts: related to unphysical gauge bosons *)
  U[11] == { ClassName->ghB,  Unphysical->True, SelfConjugate->False, Ghost->B  },
  U[12] == { ClassName->ghWi, Unphysical->True, SelfConjugate->False, Ghost->Wi, Indices->{Index[SU2W]}, FlavorIndex->SU2W} ,
  U[13] == { ClassName->ghBp, Unphysical->True, SelfConjugate->False, Ghost->Bp },

  (* Fermions: physical fields *)
  F[1] == {
    ClassName->vl, ClassMembers->{v1,v2,v3}, SelfConjugate->True, Indices->{Index[Generation]}, FlavorIndex->Generation,
    Mass->0, Width->0, PDG->{12,14,16}, ParticleName->{"v1","v2","v3"}
  },
  F[2] == {
    ClassName->Nl, ClassMembers->{N1,N2,N3}, SelfConjugate->True, Indices->{Index[Generation]}, FlavorIndex->Generation,
    Mass->0, Width->0, PDG->{9000012,9000014,9000016}, ParticleName->{"N1","N2","N3"}
  },
  F[3] == {
    ClassName->l, ClassMembers->{e,m,ta}, SelfConjugate->False, Indices->{Index[Generation]}, FlavorIndex->Generation, QuantumNumbers->{Q->-1},
    Mass->{Ml,Me,MM,MTA}, Width->0, PDG->{11,13,15}, ParticleName->{"e-","mu-","tau-"}, AntiParticleName->{"e+","mu+","tau+"}
  },
  F[4] == {
    ClassName->uq, ClassMembers->{u,c,t}, SelfConjugate->False, Indices->{Index[Generation],Index[Colour]}, FlavorIndex->Generation, QuantumNumbers->{Q->2/3},
    Mass->{Muq,MU,MC,MT}, Width->{0, 0, {WT,1.50833649}}, PDG->{2,4,6}, ParticleName->{"u","c","t"}, AntiParticleName->{"u~","c~","t~"}
  },
  F[5] == {
    ClassName->dq, ClassMembers->{d,s,b}, SelfConjugate->False, Indices->{Index[Generation],Index[Colour]}, FlavorIndex->Generation, QuantumNumbers->{Q->-1/3},
    Mass->{Mdq,MD,MS,MB}, Width->0, PDG->{1,3,5}, ParticleName->{"d","s","b"}, AntiParticleName->{"d~","s~","b~"}
  },

  (* Fermions: unphysical fields *)
  F[11] == {
    ClassName->LL, SelfConjugate->False, Unphysical->True, Indices->{Index[SU2D],Index[Generation]}, FlavorIndex->SU2D,
    QuantumNumbers->{Y->-1/2, Yp->-1}
  },
  F[12] == {
    ClassName->lR, SelfConjugate->False, Unphysical->True, Indices->{Index[Generation]}, FlavorIndex->Generation,
    QuantumNumbers->{Y->-1, Yp->-1}
  },
  F[13] == {
    ClassName->vR, SelfConjugate->False, Unphysical->True, Indices->{Index[Generation]}, FlavorIndex->Generation,
    QuantumNumbers->{Yp->-1}
  },
  F[21] == {
    ClassName->QL, SelfConjugate->False, Unphysical->True, Indices->{Index[SU2D],Index[Generation], Index[Colour]}, FlavorIndex->SU2D,
    QuantumNumbers->{Y->1/6, Yp->1/3}
  },
  F[22] == {
    ClassName->uR, SelfConjugate->False, Unphysical->True, Indices->{Index[Generation], Index[Colour]}, FlavorIndex->Generation,
    QuantumNumbers->{Y->2/3, Yp->1/3}
  },
  F[23] == {
    ClassName->dR, SelfConjugate->False, Unphysical->True, Indices->{Index[Generation], Index[Colour]}, FlavorIndex->Generation,
    QuantumNumbers->{Y->-1/3, Yp->1/3}
  },

  (* Higgs: physical scalars  + Goldstones *)
  S[1] == { ClassName->h1, SelfConjugate->True, Mass->Mh1, Width->{Wh1,0.00407}, PDG->25 },
  S[2] == { ClassName->h2, SelfConjugate->True, Mass->Mh2, Width->{Wh2,11.},     PDG->35 },

  S[3] == { ClassName->G0,  SelfConjugate->True,  Mass->MZ,  Width->{WZ,  2.4952}, Goldstone->Z,  PDG->250},
  S[4] == { ClassName->GP,  SelfConjugate->False, Mass->MW,  Width->{WW,  2.085},  Goldstone->W,  PDG->251, QuantumNumbers->{Q->1}},
  S[5] == { ClassName->G0p, SelfConjugate->True,  Mass->MZp, Width->{WZp, 10.},    Goldstone->Zp, PDG->252},

  (* Higgs: unphysical scalars  *)
  S[11] == { ClassName->phi, Unphysical->True, SelfConjugate->False, Indices->{Index[SU2D]}, FlavorIndex->SU2D, QuantumNumbers->{Y-> 1/2} },
  S[12] == { ClassName->chi, Unphysical->True, SelfConjugate->False, QuantumNumbers->{Yp->2} }
};


(* ************************** *)
(* *****   Parameters   ***** *)
(* ************************** *)
M$Parameters = {
  (* Gauge sector *)
  g1 == {
    ParameterType -> External, Value -> 0.3581, InteractionOrder -> {QED,1}, BlockName -> GAUGE, OrderBlock -> 1,
    TeX -> Subscript[g,1], Description -> "U(1)Y coupling constant at the Z pole"
  },
  gw == {
    ParameterType -> External, Value -> 0.6484, InteractionOrder -> {QED,1}, BlockName -> GAUGE, OrderBlock -> 2,
    TeX -> Subscript[g,w], Description -> "Weak coupling constant at the Z pole"
  },
  gs == {
    ParameterType -> External, Value -> 1.2198, InteractionOrder -> {QCD,1}, BlockName -> GAUGE, OrderBlock -> 3,
    TeX -> Subscript[g,s], ParameterName -> G, Description -> "Strong coupling constant at the Z pole"
  },
  gp == {
    ParameterType -> External, Value -> 0.2,    InteractionOrder -> {QED,1}, BlockName -> GAUGE, OrderBlock -> 4,
    TeX -> g', Description -> "U(1)' coupling constant"
  },

  (* Scalar sector *)
  lam1 == {
    ParameterType -> External, Value -> 0.12, InteractionOrder -> {QED, 2}, BlockName -> HIGGS, OrderBlock -> 1,
    TeX -> Subscript[\[Lambda], 1], Description -> "Higgs quartic coupling (phi^4)"
  },
  lam2 == {
    ParameterType -> External, Value -> 0.13, InteractionOrder -> {QED, 2}, BlockName -> HIGGS, OrderBlock -> 2,
    TeX -> Subscript[\[Lambda], 2], Description -> "Higgs quartic coupling (chi^4)"
  },
  lam3 == {
    ParameterType -> External, Value -> 0.14, InteractionOrder -> {QED, 2}, BlockName -> HIGGS, OrderBlock -> 3,
    TeX -> Subscript[\[Lambda], 3], Description -> "Higgs quartic coupling (phi^2 chi^2)"
  },
  mu1 == {
    ParameterType -> External, Value -> 88.4, BlockName -> HIGGS, OrderBlock -> 4,
    TeX -> Subscript[\[Mu], 1], Description -> "Higgs mass term (phi^2)"
  },
  mu2 == {
    ParameterType -> External, Value -> 89.4, BlockName -> HIGGS, OrderBlock -> 5,
    TeX -> Subscript[\[Mu], 2], Description -> "Higgs mass term (chi^2)"
  },

  vphi == {
    ParameterType -> External, Value -> 246.22, InteractionOrder -> {QED,-1}, BlockName -> HIGGS, OrderBlock -> 6,
    TeX -> Subscript[v, \[Phi]], Description -> "Doublet Higgs vev"
  },
  vchi == {
    ParameterType -> External, Value -> 500.,   InteractionOrder -> {QED,-1}, BlockName -> HIGGS, OrderBlock -> 7,
    TeX -> Subscript[v, \[Chi]], Description -> "Singlet Higgs vev"
  },

  (* Yukawa sector *)
  Ryl == {
    ParameterType -> External,  BlockName -> LYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Lepton Yukawa matrix (real part)",
    Value -> {Ryl[1,1] -> 2.93502*10^-6, Ryl[2,2] -> 0.000606878, Ryl[3,3] -> 0.0102065, Ryl[i_,j_] :> 0  /; i!=j}
  },
  Iyl == {
    ParameterType -> External, BlockName -> IMLYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Lepton Yukawa matrix (imaginary part)",
    Value -> {Iyl[i_,j_] -> 0}
  },
  Ryv == {
    ParameterType -> External,  BlockName -> VYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Neutrino Yukawa matrix (real part, phi)",
    Value -> {Ryv[1,1] -> 1.0*10^-9, Ryv[2,2] -> 1.1*^-9, Ryv[3,3] -> 1.2*^-9, Ryv[i_,j_] :> 0  /; i!=j}
  },
  Iyv == {
    ParameterType -> External, BlockName -> IMVYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Neutrino Yukawa matrix (imaginary part, phi)",
    Value -> {Iyv[i_,j_] -> 0}
  },
  RyN == {
    ParameterType -> External,  BlockName -> NYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Neutrino Yukawa matrix (real part, chi)",
    Value -> {RyN[1,1] -> 1.3*10^-9, RyN[2,2] -> 1.4*^-9, RyN[3,3] -> 1.5*^-9, RyN[i_,j_] :> 0  /; i!=j}
  },
  IyN == {
    ParameterType -> External, BlockName -> IMNYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Neutrino Yukawa matrix (imaginary part, chi)",
    Value -> {IyN[i_,j_] -> 0}
  },
  Ryu == {
    ParameterType -> External,  BlockName -> UYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Up quark Yukawa matrix (real part)",
    Value -> {Ryu[1,1] -> 0.0000146464, Ryu[2,2] -> 0.00729448, Ryu[3,3] -> 0.987914, Ryu[i_,j_] :> 0 /; i!=j}
  },
  Iyu == {
    ParameterType -> External, BlockName -> IMUYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Up quark Yukawa matrix (imaginary part)",
    Value -> {Iyu[i_,j_] -> 0}
  },
  Ryd == {
    ParameterType -> External,  BlockName -> DYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Down quark Yukawa matrix (real part)",
    Value -> {Ryd[1,1] -> 0.0000282008, Ryd[1,2] -> 6.53571*10^-6, Ryd[2,1] -> -0.000130973, Ryd[2,2] -> 0.000565134, Ryd[3,3] -> 0.0269953,
    Ryd[1,3] -> 0, Ryd[2,3] -> 0, Ryd[3,1] -> 0., Ryd[3,2] -> 0}
  },
  Iyd == {
    ParameterType -> External, BlockName -> IMDYUKAWA, Indices -> {Index[Generation], Index[Generation]}, Description -> "Down quark Yukawa matrix (imaginary part)",
    Value -> {Iyd[i_,j_] -> 0}
  },

  (* Internal Parameters *)
  aS    == { ParameterType -> Internal, Value -> gs^2/(4 Pi),  InteractionOrder -> {QCD,2} },

  yl == {
    ParameterType -> Internal, ComplexParameter -> True, Indices -> {Index[Generation], Index[Generation]}, TeX -> Subscript[y, l],
    Value -> {yl[i_, j_] :> Ryl[i,j] + I*Iyl[i,j]}, InteractionOrder -> {QED, 1}
  },
  yv == {
    ParameterType -> Internal, ComplexParameter -> True, Indices -> {Index[Generation], Index[Generation]}, TeX -> Subscript[y, \[Nu]],
    Value -> {yv[i_, j_] :> Ryv[i,j] + I*Iyv[i,j]}, InteractionOrder -> {QED, 1}
  },
  yN == {
    ParameterType -> Internal, ComplexParameter -> True, Indices -> {Index[Generation], Index[Generation]}, TeX -> Subscript[y, N],
    Value -> {yN[i_, j_] :> RyN[i,j] + I*IyN[i,j]}, InteractionOrder -> {QED, 1}
  },
  yu == {
    ParameterType -> Internal, ComplexParameter -> True, Indices -> {Index[Generation], Index[Generation]}, TeX -> Subscript[y, u],
    Value -> {yu[i_, j_] :> Ryu[i,j] + I*Iyu[i,j]}, InteractionOrder -> {QED, 1}
  },
  yd == {
    ParameterType -> Internal, ComplexParameter -> True, Indices -> {Index[Generation], Index[Generation]}, TeX -> Subscript[y, d],
    Value -> {yd[i_, j_] :> Ryd[i,j] + I*Iyd[i,j]}, InteractionOrder -> {QED, 1}
  }
};


(* ************************** *)
(* *****   Lagrangian   ***** *)
(* ************************** *)

LGauge := -1/4 * Block[{mu,nu,ii}, FS[B,mu,nu]^2 + FS[Wi,mu,nu,ii]^2 + FS[G,mu,nu,ii]^2 + FS[Bp,mu,nu]^2];

LFermions := Plus @@ (Block[{mu}, I*anti[#].Ga[mu].DC[#, mu]] &/@ {QL, LL, uR, dR, lR, vR});

LHiggs := Block[{mu,ii,jj},
    DC[phibar[ii],mu] DC[phi[ii],mu] + DC[chibar,mu] DC[chi,mu] \
  - lam1 phibar[ii] phi[ii] phibar[jj] phi[jj] - lam2 chibar chi chibar chi - lam3 phibar[ii] phi[ii] chibar chi \
  - mu1 phibar[ii] phi[ii] - mu2 chibar chi
];

LYukawa := Block[{sp,ff1,ff2,ii,jj,lag},
  lag = - yd[ff1,ff2] QLbar[sp,ii,ff1,cc].dR[sp,ff2,cc] phi[ii] \
        - yu[ff1,ff2] QLbar[sp,ii,ff1,cc].uR [sp,ff2,cc] phibar[jj] Eps[ii,jj] \
        - yl[ff1,ff2] LLbar[sp,ii,ff1].lR[sp,ff2] phi[ii] \
        - yv[ff1,ff2] LLbar[sp,ii,ff1].vR[sp,ff2] phibar[jj] Eps[ii,jj] \
        - yN[ff1,ff2] CC[vRbar][sp,ff1].vR[sp,ff2] chi ;
  Return[lag + HC[lag]];
];

LZprime := LGauge + LFermions + LYukawa + LHiggs;


